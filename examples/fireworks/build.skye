import "build";
import "std/collections";
use core::compiler::Checks;

fn main(args: Array[String]) !void {
    let conf = build::Conf::default();
    
    if args.contains("release") {
        conf.checks = Checks::Release;
    }

    let buf = Array::new[char]();
    defer buf.free();

    @format(&buf, "src%main.skye", std::os::@SEPARATOR);
    try build::compileSkyeToC(buf.asString(), "fireworks.c", conf);

    if args.contains("emit-c") {
        return (!void)::Ok;
    }

    buf.clear();
    @format(&buf, "% -w ", try build::cc());
    if (conf.checks == Checks::Release) buf += "-O3 ";
    buf += "fireworks.c -lraylib ";

    if @LINUX {
        buf += "-o fireworks -lGL -lm -lpthread -ldl -lrt -lX11";
    } else if @WINDOWS {
        buf += "-o fireworks.exe -lgdi32";
    } else {
        @println("WARNING: Unsupported platform, attempting build with no external libraries");
    }

    try build::system(buf.asString());    
    let _ = build::removeFile("fireworks.c");

    if args.contains("run") {
        if @WINDOWS {
            try build::system(".\fireworks.exe");
            let _ = build::removeFile("fireworks.exe");
        } else {
            try build::system("./fireworks");
            let _ = build::removeFile("fireworks");
        }
    }

    return (!void)::Ok;
}
